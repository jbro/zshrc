# vim: ft=zsh
# Plugin manager light
if [[ ! -v zsh_loaded_plugins ]]; then
  typeset -g zsh_loaded_plugins=()
fi
if [[ ! -v zsh_loaded_snippets ]]; then
  typeset -g zsh_loaded_snippets=()
fi
if [[ ! -v _zshrc_bench_plugins ]]; then
  typeset -gA _zshrc_bench_plugins
fi

# Time plugin and snippet loading in benchmarks
local start_time=$EPOCHREALTIME
local plugindir url branch initfile subdir requires name
for var in "$@"; do
  eval "$var"
done

plugindir="${ZPLUGINDIR}/${url:t2:r}"

# Skip if required command is missing
if [[ -n "$requires" ]] && (( ! $+commands[$requires] )); then
  if [[ ! -v _plugin_skipped ]]; then
    typeset -gA _plugin_skipped
  fi
  if [ -n "$subdir" ]; then
    name="${plugindir:t2}/${subdir:t}"
  else
    name="${plugindir:t2}"
  fi
  _plugin_skipped[$name]=$requires
  return 1
fi

if [ ! -d "$plugindir" ]; then
  echo "Cloning $url into $plugindir"
  local git_opts=(--quiet --depth=1 --single-branch)

  if [ -n "$branch" ]; then
    git_opts+=(--branch "$branch" -c advice.detachedHead=false)
  fi

  if [ -n "$subdir" ]; then
    git_opts+=(--filter=blob:none --sparse --no-checkout)
  else
    git_opts+=(--recursive --shallow-submodules)
  fi

  git clone $git_opts "$url" "$plugindir"
  echo
fi

if [ -n "$subdir" ]; then
  name="${plugindir:t2}/${subdir:t}"
  zsh_loaded_snippets+="$name"
  if [ ! -d "$plugindir/$subdir" ]; then
    echo "Fetching subdir $subdir of $url into $plugindir/$subdir"
    (cd "$plugindir" && git sparse-checkout add "$subdir" && git checkout -q) &> /dev/null
    echo
  fi
  plugindir+="/$subdir"
else
  name="${plugindir:t2}"
  zsh_loaded_plugins+="$name"
fi

if [ -z "$initfile" ]; then
  # Assume zsh plugin standard
  fpath=("$plugindir" $fpath)
  source $plugindir/*.plugin.zsh([1,1])
else
  source "$plugindir/$initfile"
fi

# Note how long snippet or plugin took to load for benchmark
_zshrc_bench_plugins+=( [$name]=$(( $EPOCHREALTIME - $start_time  )) )
