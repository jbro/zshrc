# vim: ft=zsh
local cmd=$1
local comp_dir="${ZDOTDIR}/.completions"
local comp_file="${comp_dir}/_${cmd}"
local gen_cmd=${_lazy_comp_gen[$cmd]}
local ver_cmd=${_lazy_comp_ver[$cmd]}
local needs_regen=0

if [[ -f $comp_file ]]; then
  local header
  read -r header < $comp_file
  if [[ $header == '# lazy-completion: version='* ]]; then
    # Managed by us — check age
    local file_age=$(( EPOCHSECONDS - $(stat -c %Y "$comp_file" 2>/dev/null || stat -f %m "$comp_file" 2>/dev/null) ))
    if (( file_age > 72000 )); then
      local cached_version=${header#\# lazy-completion: version=}
      local current_version=$(eval $ver_cmd 2>/dev/null)
      if [[ $current_version != $cached_version ]]; then
        needs_regen=1
      else
        touch "$comp_file"
      fi
    fi
  fi
  # No lazy-completion header — not ours, leave it alone
else
  needs_regen=1
fi

if (( needs_regen )); then
  mkdir -p "$comp_dir"
  local current_version=${current_version:-$(eval $ver_cmd 2>/dev/null)}
  { echo "# lazy-completion: version=$current_version"; eval $gen_cmd } > "$comp_file"
fi

# Unregister stub, reload real completion from updated file
unfunction _lazy_completion_${cmd}
unfunction _${cmd} 2>/dev/null
autoload -Uz _${cmd}
_${cmd} "$@"
